apply plugin: 'com.android.library'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    textOutput 'stdout'
  }

  dexOptions {
    preDexLibraries = !rootProject.ext.isCi
  }

  defaultConfig {
    minSdkVersion 4
  }
}

dependencies {
  implementation dep.supportAnnotations

  testImplementation dep.junit
}

configurations {
  jar
}
android.libraryVariants.all { variant ->
  if (variant.name != 'release') return

  def task = project.tasks.create "jar${variant.name.capitalize()}", Jar
  task.description = "Create jar artifact for ${variant.name}"
  task.dependsOn variant.javaCompile
  task.from variant.javaCompile.destinationDir
  task.destinationDir = project.file("${project.buildDir}/outputs/jar")
  task.archiveName = "${project.name}-${variant.baseName}.jar"
  artifacts.add('jar', task);
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
