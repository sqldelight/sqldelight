apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

gradlePlugin {
  plugins {
    sqlDelight {
      id = 'com.squareup.sqldelight'
      implementationClass = 'com.squareup.sqldelight.gradle.SqlDelightPlugin'
    }
  }
}

configurations {
  fixtureClasspath
  bundled
}

// Append any extra dependencies to the test fixtures via a custom configuration classpath. This
// allows us to apply additional plugins in a fixture while still leveraging dependency resolution
// and de-duplication semantics.
tasks.getByName('pluginUnderTestMetadata').getPluginClasspath().from(configurations.fixtureClasspath)

dependencies {
  implementation project(':sqldelight-compiler')
  implementation project(':sqlite-migrations')
  implementation deps.kotlin.stdlib.jdk
  implementation deps.sqlitePsi
  implementation deps.intellijCore

  // Required by intellij core
  implementation deps.guava

  compileOnly gradleApi()
  implementation deps.plugins.kotlin
  implementation deps.plugins.android

  testImplementation deps.junit
  testImplementation deps.truth

  bundled deps.intellijCore

  fixtureClasspath deps.plugins.kotlin
  fixtureClasspath deps.plugins.android
}

test {
  // The integration tests require local installations of some of the runtimes.
  if (System.getenv("TRAVIS_OS_NAME") == "linux") {
    dependsOn(
        ":sqldelight-runtime:installLocally",
        ":drivers:android-driver:installLocally",
        ":drivers:sqlite-driver:installLocally",
    )
    useJUnit {
      excludeCategories 'com.squareup.sqldelight.IosTest'
    }
  } else if (System.getenv("TRAVIS_OS_NAME") == "osx") {
    dependsOn(
        ":sqldelight-runtime:installIosLocally",
    )
    useJUnit {
      includeCategories 'com.squareup.sqldelight.IosTest'
    }
  } else {
    dependsOn(
        ":sqldelight-runtime:installLocally",
        ":drivers:android-driver:installLocally",
        ":drivers:sqlite-driver:installLocally",
        ":sqldelight-runtime:installIosLocally",
    )
  }
}



jar {
  from configurations.bundled.collect {
    it.isDirectory() ? it : zipTree(it).matching{ exclude { it.path.contains('META-INF') } }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
