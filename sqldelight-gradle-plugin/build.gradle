apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'

gradlePlugin {
  plugins {
    sqlDelight {
      id = 'com.squareup.sqldelight'
      implementationClass = 'com.squareup.sqldelight.gradle.SqlDelightPlugin'
    }
  }
}

configurations {
  fixtureClasspath
  bundled
}

// Append any extra dependencies to the test fixtures via a custom configuration classpath. This
// allows us to apply additional plugins in a fixture while still leveraging dependency resolution
// and de-duplication semantics.
tasks.getByName('pluginUnderTestMetadata').
        getPluginClasspath().
        from(configurations.fixtureClasspath)

dependencies {
  implementation project(':sqldelight-compiler')
  implementation project(':sqlite-migrations')
  implementation deps.kotlin.nativeUtils
  implementation deps.sqlitePsi
  implementation deps.intellijCore
  implementation deps.intellijJavaPlugin

  // Required by intellij core
  implementation deps.guava

  compileOnly gradleApi()
  implementation deps.plugins.kotlin
  compileOnly deps.plugins.android

  testImplementation deps.junit
  testImplementation deps.truth

  bundled deps.intellijCore
  bundled deps.intellijJavaPlugin

  fixtureClasspath deps.plugins.kotlin
  fixtureClasspath deps.plugins.android
}

test {
  // The integration tests require local installations of artifacts.
  dependsOn(
          ":runtime:publishAllPublicationsToInstallLocallyRepository",
          ":drivers:android-driver:publishAllPublicationsToInstallLocallyRepository",
          ":drivers:sqlite-driver:publishAllPublicationsToInstallLocallyRepository",
          ":drivers:jdbc-driver:publishAllPublicationsToInstallLocallyRepository",
          ":drivers:native-driver:publishAllPublicationsToInstallLocallyRepository",
          ":sqlite-migrations:publishAllPublicationsToInstallLocallyRepository",
          ":sqldelight-compiler:publishAllPublicationsToInstallLocallyRepository",
          ":sqldelight-gradle-plugin:publishAllPublicationsToInstallLocallyRepository",
  )
  useJUnit {
    if (project.hasProperty("Instrumentation")) {
      includeCategories 'com.squareup.sqldelight.Instrumentation'
    } else {
      excludeCategories 'com.squareup.sqldelight.Instrumentation'
    }
  }
}

jar {
  from {
    configurations.bundled.collect {
      it.isDirectory() ? it : zipTree(it).matching { exclude { it.path.contains('META-INF') } }
    }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
