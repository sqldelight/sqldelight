apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'java-gradle-plugin'

apply from: "$rootDir/gradle/dependencies.gradle"

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
    showExceptions true
    showStackTraces true
    showCauses true
  }
}

configurations {
  fixtureClasspath
}

// Append any extra dependencies to the test fixtures via a custom configuration classpath. This
// allows us to apply additional plugins in a fixture while still leveraging dependency resolution
// and de-duplication semantics.
def metadata = tasks.getByName('pluginUnderTestMetadata')
metadata.setPluginClasspath(metadata.getPluginClasspath() + configurations.fixtureClasspath)

dependencies {
  compileOnly gradleApi()
  implementation project(':sqldelight-compiler')
  implementation deps.kotlin.stdlib
  implementation deps.plugins.android

  testImplementation deps.junit
  testImplementation deps.truth

  fixtureClasspath deps.plugins.kotlin
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
