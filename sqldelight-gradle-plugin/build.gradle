import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
  alias(deps.plugins.kotlin.jvm)
  alias(deps.plugins.shadow) apply false
  alias(deps.plugins.publish)
  alias(deps.plugins.dokka)
  id("java-gradle-plugin")
}

gradlePlugin {
  plugins {
    sqlDelight {
      id = 'app.cash.sqldelight'
      implementationClass = 'app.cash.sqldelight.gradle.SqlDelightPlugin'
    }
  }
}

configurations {
  bundled
  shade
}

dependencies {
  api deps.sqlPsi

  implementation deps.sqliteJdbc
  implementation deps.objectDiff
  implementation deps.schemaCrawler.tools
  implementation deps.schemaCrawler.sqlite

  shade project(':sqlite-migrations')
  shade project(':sqldelight-compiler')
  shade deps.intellij.analysis
  shade deps.intellij.core
  shade deps.intellij.java
  shade deps.intellij.lang
  shade deps.intellij.testFramework

  compileOnly gradleApi()
  implementation deps.kotlin.plugin
  compileOnly deps.android.plugin

  testImplementation deps.sqlPsi
  testImplementation project(':sqlite-migrations')
  testImplementation project(':sqldelight-compiler')
  testImplementation deps.junit
  testImplementation deps.truth
}

test {
  // The integration tests require local installations of artifacts.
  dependsOn(
      ":dialects:mysql:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:hsql:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:postgresql:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:sqlite-3-18:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:sqlite-3-24:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:sqlite-3-25:publishAllPublicationsToInstallLocallyRepository",
      ":sqldelight-compiler:dialect:publishAllPublicationsToInstallLocallyRepository",
      ":runtime:publishAllPublicationsToInstallLocallyRepository",
      ":drivers:android-driver:publishAllPublicationsToInstallLocallyRepository",
      ":drivers:sqlite-driver:publishAllPublicationsToInstallLocallyRepository",
      ":drivers:jdbc-driver:publishAllPublicationsToInstallLocallyRepository",
      ":drivers:native-driver:publishAllPublicationsToInstallLocallyRepository",
      ":sqlite-migrations:publishAllPublicationsToInstallLocallyRepository",
      ":sqldelight-compiler:publishAllPublicationsToInstallLocallyRepository",
      ":sqldelight-gradle-plugin:publishAllPublicationsToInstallLocallyRepository",
  )
  useJUnit {
    if (project.hasProperty("Instrumentation")) {
      includeCategories 'app.cash.sqldelight.Instrumentation'
    } else {
      excludeCategories 'app.cash.sqldelight.Instrumentation'
    }
  }
}

if (System.getenv("CI") == "true") {
  apply plugin: 'com.github.johnrengelman.shadow'

  configurations.compileOnly.extendsFrom(configurations.shade)

  tasks.getByName("shadowJar").configure {
    archiveClassifier.set("")
    minimize()
    configurations = [project.configurations.shade]

    include '*.jar'
    include '**/*.class'
    include 'META-INF/gradle-plugins/*'
    include 'messages/*.properties'

    exclude '/groovy**'
    exclude '/kotlin/**'
  }

  artifacts {
    runtimeOnly(shadowJar)
    archives(shadowJar)
  }
} else {
  configurations.implementation.extendsFrom(configurations.shade)
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
