import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.sqldelight)
  alias(libs.plugins.android.library)
}

sqldelight {
  databases {
    SqlTestDb {
      packageName = "app.sqltest.shared"
      generateAsync = true
      verifyMigrations = true
    }
  }
}

kotlin {
  jvmToolchain(11)

  targets {
    android()
    jvm()
  }

  sourceSets {
    commonMain {
      dependencies {
        // coroutines
        api libs.kotlin.coroutines.core

        // database extension
        implementation "app.cash.sqldelight:coroutines-extensions:${app.cash.sqldelight.VersionKt.VERSION}"
        implementation "app.cash.sqldelight:async-extensions:${app.cash.sqldelight.VersionKt.VERSION}"
      }
    }
    commonTest {
      dependencies {
        implementation(kotlin("test"))

        // coroutines
        implementation libs.kotlin.coroutines.test
      }
    }

    androidMain {
      dependencies {
        // sql
        implementation("app.cash.sqldelight:android-driver:${app.cash.sqldelight.VersionKt.VERSION}")
      }
    }
    androidUnitTest {
      dependencies {
        implementation libs.junit
        implementation libs.robolectric
      }
    }

    jvmMain {
      dependencies {
        // sql
        implementation("app.cash.sqldelight:sqlite-driver:${app.cash.sqldelight.VersionKt.VERSION}")
      }
    }
    jvmTest { }
  }
}

android {
  namespace "app.sqltest.shared"

  compileSdk libs.versions.compileSdk.get() as int

  defaultConfig {
    minSdk libs.versions.minSdk.get() as int
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }
  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }
}
