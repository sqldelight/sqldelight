buildscript {
  apply from: "${projectDir.absolutePath}/../../../../gradle/dependencies.gradle"

  repositories {
    maven {
      url "file://${projectDir.absolutePath}/../../../../build/localMaven"
    }
    mavenCentral()
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.squareup.sqldelight:gradle-plugin:+'
    classpath deps.plugins.kotlin
    classpath deps.plugins.android
  }
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'com.android.library'

apply from: '../../../../gradle/dependencies.gradle'

repositories {
  maven {
    url "file://${projectDir.absolutePath}/../../../../build/localMaven"
  }
  mavenCentral()
  google()
}

android {
  compileSdkVersion versions.compileSdk

  defaultConfig {
    minSdkVersion versions.minSdk
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
  }

  lintOptions {
    textOutput 'stdout'
    textReport true
  }

  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }
}

sqldelight {
  QueryWrapper {
    packageName = "com.squareup.sqldelight.integration"
  }
}

kotlin {
  targets {
    targetFromPreset(presets.android, 'android')
  }

  sourceSets {
    commonTest {
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.kotlin.test.commonAnnotations
        implementation deps.stately.core
        implementation deps.stately.concurrency
        implementation "com.squareup.sqldelight:runtime:${com.squareup.sqldelight.VersionKt.VERSION}"
      }
    }
    androidMain {
      dependencies {
        implementation "com.squareup.sqldelight:sqlite-driver:${com.squareup.sqldelight.VersionKt.VERSION}"
      }
    }
    androidTest {
      dependencies {
        implementation deps.kotlin.test.junit
        implementation "com.squareup.sqldelight:sqlite-driver:${com.squareup.sqldelight.VersionKt.VERSION}"
      }
    }
  }
}
