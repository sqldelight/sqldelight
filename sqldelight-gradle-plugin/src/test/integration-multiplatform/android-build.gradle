plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.multiplatform'
  id 'com.squareup.sqldelight'
}

apply from: '../../../../gradle/dependencies.gradle'

repositories {
  maven {
    url "file://${projectDir.absolutePath}/../../../../build/localMaven"
  }
  mavenCentral()
  google()
  jcenter()
}

android {
  compileSdkVersion versions.compileSdk

  defaultConfig {
    minSdkVersion versions.minSdk
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
  }

  lintOptions {
    textOutput 'stdout'
    textReport true
  }

  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }
}

sqldelight {
  packageName = "com.squareup.sqldelight.integration"
  className = "QueryWrapper"
}

kotlin {
  targets {
    fromPreset(presets.android, 'android')
  }

  sourceSets {
    commonMain {
      dependencies {
        implementation deps.kotlin.stdlib.common
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.kotlin.test.commonAnnotations
        implementation deps.stately
        implementation "com.squareup.sqldelight:runtime:${com.squareup.sqldelight.VersionKt.VERSION}"
      }
    }
    androidMain {
      dependencies {
        implementation deps.kotlin.stdlib.jdk
        implementation "com.squareup.sqldelight:sqlite-driver:${com.squareup.sqldelight.VersionKt.VERSION}"
      }
    }
    androidTest {
      dependencies {
        implementation deps.kotlin.test.junit
        implementation "com.squareup.sqldelight:sqlite-driver:${com.squareup.sqldelight.VersionKt.VERSION}"
      }
    }
  }
}
