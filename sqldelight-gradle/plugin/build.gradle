apply plugin: 'kotlin'
apply plugin: 'java-gradle-plugin'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

configurations {
  fixtureClasspath
  bundled
}

// Append any extra dependencies to the test fixtures via a custom configuration classpath. This
// allows us to apply additional plugins in a fixture while still leveraging dependency resolution
// and de-duplication semantics.
def metadata = tasks.getByName('pluginUnderTestMetadata')
metadata.setPluginClasspath(metadata.getPluginClasspath() + configurations.fixtureClasspath)

dependencies {
  implementation project(':sqldelight-core')
  implementation project(':sqlite-migrations')
  implementation deps.kotlin.stdlib
  implementation deps.sqlitePsi
  implementation deps.intellijCore

  // Required by intellij core
  implementation deps.guava

  compileOnly gradleApi()
  compileOnly deps.plugins.kotlin

  testImplementation deps.junit
  testImplementation deps.truth

  bundled deps.intellijCore

  fixtureClasspath deps.plugins.kotlin
  fixtureClasspath deps.plugins.android
}

jar {
  from configurations.bundled.collect {
    it.isDirectory() ? it : zipTree(it).matching{ exclude { it.path.contains('META-INF') } }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"