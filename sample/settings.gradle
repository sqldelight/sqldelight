pluginManagement {
  resolutionStrategy {
    eachPlugin {
      if (requested.id.namespace == 'app.cash') {
        useModule('app.cash.sqldelight:gradle-plugin:+')
      }
    }
  }
  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
    maven { url "../build/localMaven" }
  }
}

dependencyResolutionManagement {
  versionCatalogs {
    deps {
      from(files("../gradle/libs.versions.toml"))
    }
  }
  repositories {
    mavenCentral()
    google()
    maven { url "../build/localMaven" }
  }
}

rootProject.name = "sample"

include ':android'
include ':common'
include ':web'

includeBuild('..') {
  dependencySubstitution {
    substitute module("app.cash.sqldelight:primitive-adapters") with project(":adapters:primitive-adapters")
    substitute module("app.cash.sqldelight:sqlite-driver") with project(":drivers:sqlite-driver")
    substitute module("app.cash.sqldelight:android-driver") with project(":drivers:android-driver")
    substitute module("app.cash.sqldelight:native-driver") with project(":drivers:native-driver")
    substitute module("app.cash.sqldelight:sqljs-driver") with project(":drivers:sqljs-driver")
    substitute module("app.cash.sqldelight:runtime") with project(":runtime")
    substitute module("app.cash.sqldelight:runtime-jvm") with project(":runtime")
    substitute module("app.cash.sqldelight:gradle-plugin") with project(":sqldelight-gradle-plugin")
    substitute module("app.cash.sqldelight:sqlite-3-18-dialect") with project(":dialects:sqlite-3-18")
  }
}