plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.kotlin.native.cocoapods)
  id("app.cash.sqldelight")
}

base {
  archivesName = 'sample-common'
}

sqldelight {
  databases {
    HockeyDb {
      packageName = "com.example.sqldelight.hockey"
    }
  }
  linkSqlite = true
}

version = 1.0

kotlin {
  applyDefaultHierarchyTemplate()
  jvmToolchain(17)

  sourceSets.commonMain.dependencies {
    implementation 'app.cash.sqldelight:primitive-adapters'
  }
  sourceSets.commonTest.dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-test'
    implementation libs.kotlinx.coroutines.test
  }

  jvm()
  sourceSets.jvmTest.dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-test-junit'
    implementation "app.cash.sqldelight:sqlite-driver"
  }

  iosArm64()
  iosX64()
  sourceSets.iosMain.dependencies {
    implementation "app.cash.sqldelight:native-driver"
  }

  cocoapods {
    summary = "Common core for SQLDelight sample."
    homepage = "https://github.com/cashapp/sqldelight/tree/master/sample/common"
  }
}
