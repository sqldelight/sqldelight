apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.native.cocoapods'
apply plugin: 'com.squareup.sqldelight'

archivesBaseName = 'sample-common'

sqldelight {
  HockeyDb {
    packageName = "com.example.sqldelight.hockey"
  }
}

version = 1.0

kotlin {
  // Configure common.
  sourceSets.commonMain.dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
  }
  sourceSets.commonTest.dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-test-common'
    implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
  }

  // Configure jvm.
  jvm()
  sourceSets.jvmTest.dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-test-junit'
    implementation "com.squareup.sqldelight:sqlite-driver:${versions.sqldelight}"
  }

  // Configure iOS.
  def sdkName = System.getenv("SDK_NAME")
  if (sdkName != null && sdkName.startsWith("iphoneos")) {
    iosArm64("ios")
  } else {
    iosX64("ios")
  }
  sourceSets.iosMain.dependencies {
    implementation "com.squareup.sqldelight:native-driver:${versions.sqldelight}"
  }
  cocoapods {
    summary = "Common core for SQLDelight sample."
    homepage = "https://github.com/cashapp/sqldelight/tree/master/sample/common"
  }
}

task iosTest {
  def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
  dependsOn 'linkDebugTestIos'
  group = JavaBasePlugin.VERIFICATION_GROUP
  description = "Runs tests for target 'ios' on an iOS simulator"

  doLast {
    def binary = kotlin.targets.ios.binaries.getTest('DEBUG').outputFile
    exec {
      commandLine 'xcrun', 'simctl', 'spawn',  '--standalone', device, binary.absolutePath
    }
  }
}

// TODO work around for https://youtrack.jetbrains.com/issue/KT-33579
task fixPathProblem(type: Copy) {
  from "$rootDir/build/js/packages/sqldelight-common-test/kotlin"
  into "$rootDir/build/js/packages/sqldelight-common-test/node_modules/kotlin"
}
task fixSourceMapProblem(type: Copy) {
  from "$rootDir/build/js/node_modules/kotlin-test-nodejs-runner/kotlin-nodejs-source-map-support.js"
  into "$rootDir/build/js/packages/sqldelight-common-test/"
}
tasks["jsTestClasses"].dependsOn("fixSourceMapProblem", "fixPathProblem")
