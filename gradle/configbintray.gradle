/*
* Copyright (C) 2018 Touchlab, Inc.
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*      http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
def ourRepo = "kotlin"
def ourOrg = "touchlabpublic"

apply plugin: 'com.jfrog.bintray'

def checkFileExists(String urlString)
{
    java.net.URL url = new java.net.URL(urlString);
    java.net.HttpURLConnection connection = (java.net.HttpURLConnection)url.openConnection();
    connection.setRequestMethod("GET");
    connection.connect();

    int code = connection.getResponseCode();
    return code <= 299;
}

bintrayUpload.doFirst {
    publications = []

    def pubs = project.extensions.getByName("publishing").getPublications()

    for(pub in pubs)
    {
        String pubUrl = "https://dl.bintray.com/${ourOrg}/${ourRepo}/" + pub.groupId.replace(".", "/") +"/"+ pub.artifactId +"/"+ pub.version +"/"+ pub.artifactId +"-"+ pub.version +".pom"
        if(checkFileExists(pubUrl)){
            logger.warn("Publication exists "+ pubUrl)
        }
        else{
            logger.warn("Publication added "+ pubUrl)
            publications += pub.name
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : ''
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : ''
    publish = true
    override = true

    pkg {
        repo = ourRepo
        name = POM_ARTIFACT_ID
        userOrg = ourOrg

        licenses = ['Apache-2.0']
        vcsUrl = POM_SCM_URL
        version {
            name = version
        }
    }
}