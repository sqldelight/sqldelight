apply plugin: "com.vanniktech.maven.publish"
apply plugin: "org.jetbrains.dokka"

mavenPublish {
  useLegacyMode = false

  targets {
    installArchives {
      releaseRepositoryUrl = "file://${rootProject.buildDir}/localMaven"
      snapshotRepositoryUrl = "file://${rootProject.buildDir}/localMaven"
    }
  }
}

project.tasks.dokka {
  outputDirectory = "$rootDir/docs/1.x"
  outputFormat = 'gfm'

  impliedPlatforms = ["Common"] // This will force platform tags for all non-common sources e.g. "JVM"
  kotlinTasks {
    // dokka fails to retrieve sources from MPP-tasks so they must be set empty to avoid exception
    // use sourceRoot instead (see below)
    []
  }
  packageOptions {
    prefix = "com.squareup.sqldelight.internal"
    suppress = true
  }
  sourceRoot {
    // assuming there is only a single source dir...
    path = kotlin.sourceSets.commonMain.kotlin.srcDirs[0]
    platforms = ["Common"]
  }
  if (kotlin.sourceSets.getNames().contains("jvmMain")) {
    sourceRoot {
      // assuming there is only a single source dir...
      path = kotlin.sourceSets.jvmMain.kotlin.srcDirs[0]
      platforms = ["JVM"]
    }
  }
  if (kotlin.sourceSets.getNames().contains("jsMain")) {
    sourceRoot {
      // assuming there is only a single source dir...
      path = kotlin.sourceSets.jsMain.kotlin.srcDirs[0]
      platforms = ["js"]
    }
  }
  if (kotlin.sourceSets.getNames().contains("nativeMain")) {
    sourceRoot {
      // assuming there is only a single source dir...
      path = kotlin.sourceSets.nativeMain.kotlin.srcDirs[0]
      platforms = ["native"]
    }
  }
}

def isReleaseBuild() {
  return VERSION_NAME.contains("SNAPSHOT") == false
}

def getGpgKey() {
  return hasProperty('signingKey') ? signingKey : ""
}

afterEvaluate {
  signing {
    required { isReleaseBuild() && !getGpgKey().isEmpty() }

    def signingKey = getGpgKey()
    if (!signingKey.isEmpty() && isReleaseBuild()) {
      useInMemoryPgpKeys(signingKey, "")
    }
  }
}
