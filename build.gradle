buildscript {
  dependencies {
    classpath("com.squareup.okhttp3:okhttp") {
      version {
        strictly '4.4.1'
      }
    }
  }
}

plugins {
  alias(libs.plugins.grammarKitComposer) apply false
  alias(libs.plugins.kotlin.multiplatform) apply false
  alias(libs.plugins.kotlin.jvm) apply false
  alias(libs.plugins.kotlin.android) apply false
  alias(libs.plugins.kotlin.js) apply false
  alias(libs.plugins.ksp) apply false
  alias(libs.plugins.android.application) apply false
  alias(libs.plugins.android.library) apply false
  alias(libs.plugins.publish) apply false
  alias(libs.plugins.dokka)
  alias(libs.plugins.spotless)
}

spotless {
  kotlin {
    target "**/*.kt"
    targetExclude "**/gen/**/*.*", "**/generated/**/*.*", "sqldelight-compiler/integration-tests/src/test/kotlin/com/example/**/*.*", "sqldelight-compiler/src/test/migration-interface-fixtures/**/*.*"
    ktlint(libs.versions.ktlint.get()).editorConfigOverride([
      "indent_size": "2",
       "disabled_rules": "package-name",
       "ij_kotlin_allow_trailing_comma": "true",
       "ij_kotlin_allow_trailing_comma_on_call_site": "true",
    ])
    trimTrailingWhitespace()
    endWithNewline()
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
  }

  tasks.withType(Test).configureEach {
    testLogging {
      events = ["failed", "skipped", "passed"]
      exceptionFormat "full"
    }
  }

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }

  // Workaround for yarn concurrency issue - https://youtrack.jetbrains.com/issue/KT-43320
  tasks.withType(org.jetbrains.kotlin.gradle.targets.js.npm.tasks.KotlinNpmInstallTask).configureEach {
    args.addAll(["--mutex", "file:${file("build/.yarn-mutex")}".toString()])
  }

  tasks.withType(org.jetbrains.grammarkit.tasks.GenerateParserTask).configureEach {
    doFirst {
      // https://github.com/JetBrains/gradle-grammar-kit-plugin/pull/78.
      logging.captureStandardOutput(LogLevel.INFO)
    }
  }

  // https://youtrack.jetbrains.com/issue/KT-46466
  tasks.withType(org.gradle.api.publish.maven.tasks.AbstractPublishToMaven).configureEach {
    dependsOn(tasks.withType(Sign))
  }

  configurations.all {
    exclude group: 'com.jetbrains.rd'
    exclude group: 'com.github.jetbrains', module: 'jetCheck'
    exclude group: 'org.roaringbitmap'
  }

  group = GROUP
  version = VERSION_NAME
}

tasks.named("dokkaHtmlMultiModule") {
  outputDirectory = file("$rootDir/docs/2.x")

  moduleName.set("SQLDelight")
  pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase": """
    {
      "footerMessage": "Copyright &copy; 2022 Square, Inc.",
      "customStyleSheets": ["${file("docs/css/logo-styles.css")}"]
    }
  """])
}
