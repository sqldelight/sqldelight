import org.jetbrains.intellij.tasks.RunPluginVerifierTask.FailureLevel

plugins {
  alias(deps.plugins.intellij)
  alias(deps.plugins.kotlin.jvm)
  alias(deps.plugins.changelog)
}

intellij {
  updateSinceUntilBuild = false

  // Comment version and uncomment localPath to test a local IDE build.
  version = "IC-${deps.versions.idea.get()}"
//  localPath = "/Applications/Android Studio.app"

  pluginName = 'SQLDelight'
  plugins = [
      "org.jetbrains.kotlin",
      "com.intellij.gradle",
      "com.intellij.java",
      "org.jetbrains.android"
  ]
  if (!project.version.endsWith("SNAPSHOT")) {
    patchPluginXml {
      changeNotes = (provider { changelog.get(project.version).toHTML() })
    }
  } else {
    patchPluginXml {
      version = "$VERSION_NAME-${System.currentTimeMillis()}"
    }
  }
}

runIde {
  maxHeapSize = "4g"
}

runPluginVerifier {
  ideVersions = [
      "IC-2021.1",
      "IC-2021.2.3",
      "IC-2021.3",
      "IC-2022.1",
  ]

  def customFailureLevel = FailureLevel.ALL
  customFailureLevel.remove(FailureLevel.DEPRECATED_API_USAGES)
  customFailureLevel.remove(FailureLevel.EXPERIMENTAL_API_USAGES)
  customFailureLevel.remove(FailureLevel.INTERNAL_API_USAGES)
  customFailureLevel.remove(FailureLevel.NOT_DYNAMIC)

  setFailureLevel(customFailureLevel)
}

changelog {
  path = "$rootDir/CHANGELOG.md"
}

compileKotlin {
  kotlinOptions {
    freeCompilerArgs = ['-Xjvm-default=enable']
  }
}

def isReleaseBuild() {
  return VERSION_NAME.contains("SNAPSHOT") == false
}

def getPluginToken() {
  return hasProperty('intellijPublishToken') ? intellijPublishToken : ""
}

publishPlugin {
  token = getPluginToken()
  channels = [isReleaseBuild() ? 'Stable' : 'EAP']
}

sourceSets {
  main.java.srcDir "src/generated/kotlin"
}

repositories {
  maven { url = uri("https://repo.gradle.org/gradle/libs-releases") }
}

dependencies {
  implementation project(':sqldelight-compiler')

  implementation deps.sqliteJdbc
  implementation(deps.bugsnag) {
    exclude group: "org.slf4j"
  }

  testImplementation deps.truth
  testImplementation project(':dialects:sqlite-3-18')
  testImplementation project(':test-util')
}

def getBugsnagKey() {
  return hasProperty('SQLDELIGHT_BUGSNAG_KEY') ? SQLDELIGHT_BUGSNAG_KEY : ''
}

task bugsnagKey {
  def outputDir = file("src/generated/kotlin")
  def versionFile = file("$outputDir/app.cash.sqldelight/intellij/Bugsnag.kt")

  inputs.property 'bugsnagKey', getBugsnagKey()
  outputs.file(versionFile)

  doLast {
    versionFile.parentFile.mkdirs()
    versionFile.text = """// Generated file. Do not edit!
package app.cash.sqldelight.intellij

internal val BUGSNAG_KEY = "${getBugsnagKey()}"
"""
  }
}

tasks.named('compileKotlin').configure { dependsOn('bugsnagKey') }

tasks.named('buildSearchableOptions').configure { enabled = false }
