plugins {
  alias(libs.plugins.kotlin.jvm)
  alias(libs.plugins.grammarKitComposer)
  alias(libs.plugins.publish)
  alias(libs.plugins.dokka)
}

tasks.named('test') {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
    showExceptions true
    showStackTraces true
    showCauses true
  }
}

sourceSets {
  main.java.srcDir "gen"
}

grammarKit {
  intellijRelease.set(libs.versions.idea)
}

dependencies {
  api libs.sqlPsi
  api(project(':sqldelight-compiler:dialect'))

  implementation libs.kotlinPoet
  implementation libs.jgrapht

  compileOnly libs.sqliteJdbc
  compileOnly libs.intellij.testFramework

  testImplementation libs.burst
  testImplementation libs.sqliteJdbc
  testImplementation (libs.intellij.testFramework) {
    exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-core"
  }
  testImplementation libs.kotlin.coroutines.core
  testImplementation libs.kotlin.test.junit
  testImplementation libs.truth
  testImplementation project(':test-util')
  testImplementation project(':dialects:hsql')
  testImplementation project(':dialects:mysql')
  testImplementation project(':dialects:postgresql')
  testImplementation project(':dialects:sqlite-3-18')
  testImplementation project(':dialects:sqlite-3-24')
  testImplementation project(':dialects:sqlite-3-25')
  testImplementation project(':dialects:sqlite-3-30')
  testImplementation project(':dialects:sqlite-3-33')
  testImplementation project(':dialects:sqlite-3-35')
  testImplementation project(':dialects:sqlite-3-38')
}

tasks.register('pluginVersion') {
  def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'rev-parse', '--short', 'HEAD'
      standardOutput = stdout
    }
    return stdout.toString().trim()
  }

  def outputDir = file("gen")

  inputs.property 'version', version
  outputs.dir outputDir

  doLast {
    def versionFile = file("$outputDir/app.cash.sqldelight/Version.kt")
    versionFile.parentFile.mkdirs()
    versionFile.text = """// Generated file. Do not edit!
package app.cash.sqldelight

val VERSION = "${project.version}"
val GIT_SHA = "${getGitHash}"
"""
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"

afterEvaluate {
  tasks.named('compileKotlin') { dependsOn('pluginVersion') }
  tasks.named('dokkaHtml') { dependsOn('pluginVersion') }
  tasks.named('javaSourcesJar') { dependsOn('pluginVersion') }
}
