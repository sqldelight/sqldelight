apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

archivesBaseName = 'sqldelight-android-driver'

android {
  compileSdkVersion versions.compileSdk

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
  }

  defaultConfig {
    minSdkVersion versions.minSdk
  }

  // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
  libraryVariants.all {
    it.generateBuildConfig.enabled = false
  }

  testOptions {
    unitTests.all {
      testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showStackTraces true
        showCauses true
      }
    }
  }
}

dependencies {
  implementation deps.kotlin.stdlibJdk
  implementation deps.androidx.sqliteFramework

  api project(':runtime:sqldelight-runtime:jdk')
  api deps.androidx.sqlite

  testImplementation deps.junit
  testImplementation deps.truth
  testImplementation deps.robolectric
}

configurations {
  jar
}
android.libraryVariants.all { variant ->
  if (variant.name != 'release') return

  def task = project.tasks.create "jar${variant.name.capitalize()}", Jar
  task.description = "Create jar artifact for ${variant.name}"
  task.dependsOn variant.javaCompile
  task.from variant.javaCompile.destinationDir
  task.destinationDir = project.file("${project.buildDir}/outputs/jar")
  task.archiveName = "${project.name}-${variant.baseName}.jar"
  artifacts.add('jar', task);
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
