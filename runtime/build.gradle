apply plugin: 'org.jetbrains.kotlin.multiplatform'

// TODO work around for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
  compileClasspath
}

kotlin {
  sourceSets {
    commonMain {
      dependencies {
        implementation deps.kotlin.stdlib.common
      }
    }
    commonTest {
      dependencies {
        implementation deps.stately.collections
        implementation deps.kotlin.test.common
        implementation deps.kotlin.test.commonAnnotations
      }
    }
    jvmMain {
      dependencies {
        implementation deps.kotlin.stdlib.jdk
      }
    }
    jvmTest {
      dependencies {
        implementation deps.kotlin.test.junit
      }
    }
    jsMain {
      dependencies {
        implementation deps.kotlin.stdlib.js
      }
    }
    jsTest {
      dependencies {
        implementation deps.kotlin.test.js
      }
    }
    nativeMain {
      dependencies {
        implementation deps.stately.core
        implementation deps.stately.collections
      }
    }
  }

  targets {
    targetFromPreset(presets.jvm, 'jvm')
    targetFromPreset(presets.js, 'js') {
      tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
        moduleKind = 'umd'
        sourceMap = true
        metaInfo = true
      }
    }
    targetFromPreset(presets.iosX64, 'iosX64')
    targetFromPreset(presets.iosArm32, 'iosArm32')
    targetFromPreset(presets.iosArm64, 'iosArm64')
    targetFromPreset(presets.tvosX64, 'tvosX64')
    targetFromPreset(presets.tvosArm64, 'tvosArm64')
    targetFromPreset(presets.watchosX86, 'watchosX86')
    targetFromPreset(presets.watchosArm32, 'watchosArm32')
    targetFromPreset(presets.watchosArm64, 'watchosArm64')
    targetFromPreset(presets.mingwX64, 'mingw')
  }

  configure([targets.iosX64, targets.iosArm32, targets.iosArm64, targets.tvosX64, targets.tvosArm64, targets.watchosX86, targets.watchosArm32, targets.watchosArm64, targets.mingw]) {
    compilations.main.source(sourceSets.nativeMain)
  }
}

afterEvaluate {
  // Alias the task names we use elsewhere to the new task names.
  tasks.register('install') {
    dependsOn(tasks.named('publishToMavenLocal'))
  }
  tasks.register('installLocally') {
    dependsOn(tasks.named('publishAllPublicationsToTestRepository'))
  }
  // NOTE: We do not alias uploadArchives because CI runs it on Linux and we only want to run it on Mac OS.
  //tasks.register('uploadArchives').dependsOn('publishKotlinMultiplatformPublicationToMavenRepository')
}

apply from: "$rootDir/gradle/gradle-mvn-mpp-push.gradle"

task iosTest {
  def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
  dependsOn 'linkIosX64'
  group = JavaBasePlugin.VERIFICATION_GROUP
  description = "Runs tests for target 'ios' on an iOS simulator"

  doLast {
    def binary = kotlin.targets.iosX64.binaries.getTest("DEBUG").outputDirectory.listFiles()[0]
    exec {
      commandLine 'xcrun', 'simctl', 'spawn', '--standalone', device, binary
    }
  }
}
