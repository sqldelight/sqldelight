import org.jetbrains.kotlin.gradle.plugin.KotlinCompilation

plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.publish)
  alias(libs.plugins.dokka)
}

kotlin {
  [js(), wasmJs()].forEach {
    it.browser {
      testTask {
        useKarma {
          useChromeHeadless()
        }
      }
    }
  }
  applyDefaultHierarchyTemplate {
    it.common {
      it.withJs()
      it.withWasmJs()
    }
  }

  compilerOptions {
    freeCompilerArgs.add("-Xexpect-actual-classes")
  }

  sourceSets {
    commonMain.dependencies {
      api projects.runtime
      implementation libs.kotlin.coroutines.core
    }

    commonTest.dependencies {
      implementation libs.kotlin.test
      implementation npm("sql.js", libs.versions.sqljs.get())
      implementation npm("@cashapp/sqldelight-sqljs-worker", project(":drivers:web-worker-driver:sqljs").projectDir)
      implementation devNpm("copy-webpack-plugin", "9.1.0")
      implementation libs.kotlin.coroutines.test
      implementation project(":extensions:async-extensions")
    }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"

tasks.named("dokkaHtmlMultiModule") {
  dependsOn(rootProject.tasks.named("dokkaHtmlMultiModule"))
}
