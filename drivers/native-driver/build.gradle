apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
  sourceSets {
    commonMain {
      dependencies {
        implementation deps.kotlin.stdlib.common
        api project (':runtime')
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.testhelp
      }
    }
    nativeMain {
      dependencies {
        api deps.sqliter
        implementation deps.stately.core
      }
    }
    nativeTest {
      dependencies {
        implementation project(':drivers:driver-test')
      }
    }
  }

  targets {
    targetFromPreset(presets.iosX64, 'iosX64')
    targetFromPreset(presets.iosArm32, 'iosArm32')
    targetFromPreset(presets.iosArm64, 'iosArm64')
    targetFromPreset(presets.tvosX64, 'tvosX64')
    targetFromPreset(presets.tvosArm64, 'tvosArm64')
    targetFromPreset(presets.watchosX86, 'watchosX86')
    targetFromPreset(presets.watchosArm32, 'watchosArm32')
    targetFromPreset(presets.watchosArm64, 'watchosArm64')
    targetFromPreset(presets.macosX64, 'macosX64')
    targetFromPreset(presets.mingwX64, 'mingw') {
      compilations.main.kotlinOptions.freeCompilerArgs += ["-linker-options", "-Lc:\\msys64\\mingw64\\lib"]
      compilations.test.kotlinOptions.freeCompilerArgs += ["-linker-options", "-L$projectDir\\libs".toString()]
    }
  }

  configure([targets.iosX64, targets.iosArm32, targets.iosArm64, targets.tvosX64, targets.tvosArm64, targets.watchosX86, targets.watchosArm32, targets.watchosArm64, targets.macosX64, targets.mingw]) {
    compilations.main.source(sourceSets.nativeMain)
    compilations.test.source(sourceSets.nativeTest)
    compilations.test {
      kotlinOptions.freeCompilerArgs += ["-linker-options", "-lsqlite3"]
    }
  }
}

task iosTest {
  def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
  dependsOn 'linkIosX64'
  group = JavaBasePlugin.VERIFICATION_GROUP
  description = "Runs tests for target 'ios' on an iOS simulator"

  doLast {
    def binary = kotlin.targets.iosX64.binaries.getTest("DEBUG").outputDirectory.listFiles()[0]
    exec {
      commandLine 'xcrun', 'simctl', 'spawn', '--standalone', device, binary
    }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
