apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
  sourceSets {
    commonMain {
      dependencies {
        api project (':runtime')
        api deps.sqliter
        implementation deps.stately.core
        implementation deps.stately.concurrency
        implementation deps.stately.collections
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.testhelp
        implementation project(':drivers:driver-test')
      }
    }
    nativeDarwinMain {}
    nativeDarwinTest {}
    mingwMain {}
    mingwTest {}
  }

  targets {
    targetFromPreset(presets.iosX64, 'iosX64')
    targetFromPreset(presets.iosArm32, 'iosArm32')
    targetFromPreset(presets.iosArm64, 'iosArm64')
    targetFromPreset(presets.tvosX64, 'tvosX64')
    targetFromPreset(presets.tvosArm64, 'tvosArm64')
    targetFromPreset(presets.watchosX86, 'watchosX86')
    targetFromPreset(presets.watchosArm32, 'watchosArm32')
    targetFromPreset(presets.watchosArm64, 'watchosArm64')
    targetFromPreset(presets.macosX64, 'macosX64')
    // targetFromPreset(presets.macosX64, 'nativeDarwin')
    targetFromPreset(presets.mingwX64, 'mingw') {
      compilations.main.kotlinOptions.freeCompilerArgs += ["-linker-options", "-Lc:\\msys64\\mingw64\\lib"]
      compilations.test.kotlinOptions.freeCompilerArgs += ["-linker-options", "-L$projectDir\\libs".toString()]
    }
  }

  def nativeDarwinTargets = [
    targets.iosX64, targets.iosArm32, targets.iosArm64,
    targets.tvosX64, targets.tvosArm64,
    targets.watchosX86, targets.watchosArm32, targets.watchosArm64,
    targets.macosX64
    // targets.nativeDarwin
  ]

  configure(nativeDarwinTargets) {
    compilations.main.source(sourceSets.nativeDarwinMain)
    compilations.test.source(sourceSets.nativeDarwinTest)
  }

  configure(nativeDarwinTargets + [targets.mingw]) {
    compilations.test {
      kotlinOptions.freeCompilerArgs += ["-linker-options", "-lsqlite3"]
    }
  }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
